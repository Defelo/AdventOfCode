alias e := example
alias r := run
alias t := test
alias b := bench
alias ty := test-year
alias by := bench-year
alias ta := test-all
alias ba := bench-all
alias hf := hyperfine
alias hfe := hyperfine-example

target := `cargo metadata --format-version=1 | jq -r .target_directory`

_default:
    @just --list

example year day ex:
    cargo run --bin {{year}}_{{day}} -- ../examples/{{year}}/{{trim_start_match(day, "0")}}/{{ex}}

run year day:
    cargo run --bin {{year}}_{{day}}

test year day:
    cargo test --bin {{year}}_{{day}}

bench year day:
    cargo bench --bin {{year}}_{{day}}

test-year year:
    cargo test --bin '{{year}}_*'

bench-year year:
    cargo bench --bin '{{year}}_*'

test-all:
    cargo test --bin '*'

bench-all:
    cargo bench --bin '*'

build year day:
    cargo build --release --bin {{year}}_{{day}}

hyperfine year day *args: (build year day)
    hyperfine {{args}} {{target}}/release/{{year}}_{{day}}

hyperfine-example year day ex *args: (build year day)
    hyperfine {{args}} '{{target}}/release/{{year}}_{{day}} ../examples/{{year}}/{{trim_start_match(day, "0")}}/{{ex}}'
