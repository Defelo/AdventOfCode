alias r := run
alias w := watch
alias e := example
alias we := watch-example
alias t := test
alias ty := test-year
alias ta := test-all

_default:
    @just --list

run year day:
    uiua run --no-format {{year}}/{{day}}.ua ../.cache/{{year}}/{{trim_start_match(day, "0")}}

watch year day:
    uiua watch --no-format {{year}}/{{day}}.ua ../.cache/{{year}}/{{trim_start_match(day, "0")}}

example year day ex:
    uiua run --no-format {{year}}/{{day}}.ua ../examples/{{year}}/{{trim_start_match(day, "0")}}/{{ex}}

watch-example year day ex:
    uiua watch --no-format {{year}}/{{day}}.ua ../examples/{{year}}/{{trim_start_match(day, "0")}}/{{ex}}

test year day:
    @diff <(just run {{year}} {{day}}) <(cat ../.cache/{{year}}/{{trim_start_match(day, "0")}}.{1,2})

test-year year:
    @set -e; for day in $(ls {{year}}); do just test {{year}} $(basename $day .ua); done

test-all:
    @set -e; for year in *; do [[ -d $year ]] || continue; just test-year $year; done
